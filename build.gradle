plugins {
	id 'java'
	id 'jacoco'
	id 'checkstyle'
	id 'pmd'
	id "org.owasp.dependencycheck" version "7.4.1"
	id 'org.cyclonedx.bom' version '1.7.2'
	id "com.palantir.docker" version "0.34.0"

	id 'org.springframework.boot' version '2.7.6'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.mk'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2021.0.5")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
	implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	implementation "org.mapstruct:mapstruct:1.5.3.Final"
	compileOnly 'org.projectlombok:lombok'

	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor "org.mapstruct:mapstruct-processor:1.5.3.Final"
	testAnnotationProcessor 'org.projectlombok:lombok'
	testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.5.3.Final"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

compileJava {
	options.compilerArgs += ['-Amapstruct.defaultComponentModel=spring']
}

pmd {
	ruleSets = ["rulesets/java/quickstart.xml"]
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

build{
	finalizedBy cyclonedxBom
}

docker {
	name "${project.group}/${project.name}:${project.version}"
	files "$buildDir/libs/${project.name}-${project.version}.jar"
	dockerfile file("$projectDir/Dockerfile")
}
